"Name"     = Macro
"Author"   = Robin Sam
"Version"  = 1.0
"About"    = 'Macro Grammar for GNet'

"Start Symbol" = <MacroList>
"Case Sensitive" = True
"Character Mapping" = 'Unicode'

Comment Line  = '--'

! ------------------------------------------------- Sets

{ID Head}      = {Letter} + [_]
{ID Tail}      = {Alphanumeric} + [_]
{String1 Ch} = {Printable} + {HT} + {CR} + {LF} - ["\]
{String2 Ch} = {Printable} + {HT} + {CR} + {LF} - [''\]
{Exponent} = [eE]
{Sign} = [-+]
 
! ------------------------------------------------- Terminals

Identifier    = {ID Head}{ID Tail}*
Integer = {Sign}?{Whitespace}*{Digit}+({Exponent}{Sign}?{Digit}+)?
Decimal = {Sign}?{Whitespace}*{Digit}*'.'{Digit}+({Exponent}{Sign}?{Digit}+)?
! String  = '"' ( {String1 Ch} | '\' {Printable} )* '"'
! use the following to support string with either " or '
String  = '"' ( {String1 Ch} | '\' {Printable} )* '"' | ''  ( {String2 Ch} | '\' {Printable} )* ''

! ------------------------------------------------- Rules

<MacroList>			::= <Macro>
				  | <Macro> <MacroList>

<Macro>			::= macro '[' <MacroParts> ']'

<MacroParts>			::= <MacroPart>
				  | <MacroPart> <MacroParts>

<MacroPart>			::= name String
				  | steps <StepList>
				  | cool Integer <TimeUnit>

<TimeUnit>			::= ms
				  | s
				  | m
				  | msec
				  | sec
				  | min

<StepList>			::= <Step>
				  | <Step> <StepList>

<Step>				::= <DownStep>
				  | <UpStep>
				  | <TapStep>
				  | <Delay>
				  | <FunctionCall>
				  | <Macro>

<DownStep>			::= down <KeyStepList>

<UpStep>			::= up <KeyStepList>

<TapStep>			::= tap <KeyStepList>

<Delay>			::= delay Integer <TimeUnit>

<KeyStepList>			::= <KeyStep>
				  | <KeyStep> ',' <KeyStepList>

<KeyStep>			::= Identifier
				  | cool Integer <TimeUnit>

<FunctionCall>		::= Identifier '(' <FunctionArgList> ')'
				  | Identifier '(' ')'

<FunctionArgList>		::= <FunctionArg>
				  | <FunctionArg> ',' <FunctionArgList>

<FunctionArg>			::= <FunctionCall>
				  | Integer
				  | Decimal
				  | String
				  | true
				  | false
				  | null
				  | Identifier
